# боевой_интерфейс
!Displays information about the battle and player options
!Most of the time you are on this location when fighting

if текущий < 1: текущий = количество_врагов
if текущий > количество_врагов: текущий = 1

!switching an enemy if he is dead
IF враг[текущий] ! 1:
 i = 0
 :loop
 i += 1
 if враг[i]!1: jump 'loop'
 текущий = i
END

!enemy statuses
$плохие_эффекты_проивника = ''
$хорошие_эффекты_противника = ''

gs '#dynamic'
dynamic $слабость_противника
dynamic $скованность_противника
dynamic $боль_противника
dynamic $шаткость_противника
dynamic $ошеломление_противника
dynamic $рассеянность_противника

dynamic $бешенство_противника
dynamic $разгон_противника
dynamic $меткость_противника
dynamic $адреналин_противника
dynamic $верткость_противника
dynamic $внимание_противника

$эффекты_противника = $хорошие_эффекты_противника + $плохие_эффекты_проивника

!player statuses
$плохие_эффекты_игрока = ''
$хорошие_эффекты_игрока = ''
gs '#dynamic'
dynamic $слабость_игрока
dynamic $скованность_игрока
dynamic $боль_игрока
dynamic $шаткость_игрока
dynamic $ошеломление_игрока
dynamic $рассеянность_игрока

dynamic $бешенство_игрока
dynamic $разгон_игрока
dynamic $меткость_игрока
dynamic $адреналин_игрока
dynamic $верткость_игрока
dynamic $внимание_игрока

$эффекты_игрока = $хорошие_эффекты_игрока + $плохие_эффекты_игрока

!Information about the latest actions of the player and enemies
$информация_о_бое = ''
if $боевая_обстановка!'': $информация_о_бое += '<<$боевая_обстановка>><br><br>'
$информация_о_бое += '<<$действия_игрока>><br><br>'
$информация_о_бое += '<<$действия_врага[1]>>'
$информация_о_бое += '<<$действия_врага[2]>>'
$информация_о_бое += '<<$действия_врага[3]>>'
$информация_о_бое += '<<$действия_врага[4]>>'
$информация_о_бое += '<<$действия_врага[5]>>'


!*p '<div id = "fight_bg"><img src = "content\pic\bg\<<$bg[$place]>>.jpg"></div>'
IF ui_style = 2: 
 *p '<div id = "fight_bg"><img src = "content\pic\ui\grimdark\bg\fight.jpg"></div>'
 else
 *p '<div id = "fight_bg"><img src = "content\pic\bg\fight.jpg"></div>'
END
*p '<div id = "fight_txt"><<$информация_о_бое>></div>'

IF arena_mode = 0:
 IF ui_style = 2:
  *p '<div id = "fight_options_ui2"><a href = "EXEC: dynamic $в_боевое_меню">Regular attacks</a>
<a href="EXEC: gt ''суперудары''">Special techniques</a>
<a href = "EXEC: gt ''инвентарь''">Use potion</a>
<a href = "EXEC: gt ''сменить_оружие''">Change weapon</a>
<a href="EXEC: gt ''передышка''">Defend</a>
</div>'
 else
 *p '<div id = "fight_options"><a href = "EXEC: dynamic $в_боевое_меню">Regular attacks</a>
<a href="EXEC: gt ''суперудары''">Special techniques</a>
<a href = "EXEC: gt ''инвентарь''">Use potion</a>
<a href = "EXEC: gt ''сменить_оружие''">Change weapon</a>
<a href="EXEC: gt ''передышка''">Defend</a>
</div>'
 END
 else
 if суперудар_сделан[1] = 0: 
  IF ui_style = 2: 
   *p "<div id = 'fight_options_ui2'><a href = 'EXEC: dynamic $суперудар_1'><img src = '<<$extra_special_active>>'></a></div>"
   else
   *p "<div id = 'fight_options'><a href = 'EXEC: dynamic $суперудар_1'><img src = '<<$extra_special_active>>'></a></div>"
  END
 END
 if суперудар_сделан[1] = 1: 
  IF ui_style = 2: 
   *p "<div id = 'fight_options_ui2'><img src = '<<$extra_special_used>>'></div>"
   else
   *p "<div id = 'fight_options'><img src = '<<$extra_special_used>>'></div>"
  END
 END
END

*p '<div id = "fight_subtions"><<$боевое_меню>></div>'

*p '<div id = "ally_pic"><img src = "<<$combatant_ava>>"></div>'
*p '<div id = "enemy_pic"><img src = "<<$аватара_моба[$враг[текущий]]>>"></div>'

*p '<div id = "ally_name"><<$combatant_name>></div>'
*p '<div id = "enemy_name"><<$имя_врага[текущий]>></div>'
if количество_врагов > 1: *p '<div id = "next_enemy"><a href="EXEC: dynamic $next_enemy_click"><img src = "content/pic/buttons/next.png"></a></div>'
if количество_врагов > 1: *p '<div id = "prev_enemy"><a href="EXEC: dynamic $prev_enemy_click"><img src = "content/pic/buttons/prev.png"></a></div>'

*p '<div id = "ally_def"><<$защита_описание[$защита[0]]>></div>'
*p '<div id = "next_def"><a href="EXEC: dynamic $сменить_защиту"><img src = "content/pic/buttons/loop.png"></a></div>'
*p '<div id = "enemy_def"><<$защита_врага_описание[$защита[текущий]]>></div>'

!Fixed code to display a tooltip when you click on the heart
*p '<div id = "ally_health"><img src = "content/pic/hart_red.png"></div>'
*p '<div id = "ally_health"><a style = "color: black;" href="EXEC: msg $живучесть_справка"><<здоровье_игрока>></a></div>'
*p '<div id = "ally_stamina"><img src = "content/pic/hart_green.png"></div>'
*p '<div id = "ally_stamina"><a style = "color: black;" href="EXEC: msg $выносливость_справка"><<выносливость_игрока>></a></div>'
*p '<div id = "ally_will"><img src = "content/pic/hart_purple.png"></div>'
*p '<div id = "ally_will"><a style = "color: black;" href="EXEC: msg $боевой_дух_справка"><<дух_игрока>></a></div>'
*p '<div id = "enemy_health"><img src = "content/pic/hart_red.png"></div>'
*p '<div id = "enemy_health"><center><<здоровье_врага[текущий]>></center></div>'
*p '<div id = "enemy_stamina"><img src = "content/pic/hart_green.png"></div>'
*p '<div id = "enemy_stamina"><center><<выносливость_врага[текущий]>></center></div>'
*p '<div id = "enemy_will"><img src = "content/pic/hart_purple.png"></div>'
*p '<div id = "enemy_will"><center><<дух_врага[текущий]>></center></div>'


*p '<div id = "ally_conditions"><a href="EXEC: msg $хиты_справка">STATUS</a>
<<$эффекты_игрока>>
</div>'
*p '<div id = "enemy_conditions"><a href="EXEC: msg $хиты_справка">STATUS</a>
<<$эффекты_противника>>
</div>'

IF $music_now ! $music[$toplay]:
  close $music_now
  $music_now = $music[$toplay]
END
if sound_on = 1: play $music_now, music_volume

IF ui_style = 2:
 IF sound_on = 1:
  *p '<div id="sound_icon"><a href = "EXEC: sound_on = 0 & close all & gt curloc"><img src="content\pic\ui\grimdark\buttons\sound_on.png"></a></div>'  
  else
  *p '<div id="sound_icon"><a href = "EXEC: sound_on = 1 & gt curloc"><img src="content\pic\ui\grimdark\buttons\sound_off.png"></a></div>'  
 END
 ELSE
 IF sound_on = 1:
  *p '<div id="sound_icon"><a href = "EXEC: sound_on = 0 & close all & gt curloc"><img src="content\pic\buttons\sound_on.png"></a></div>'  
  else
  *p '<div id="sound_icon"><a href = "EXEC: sound_on = 1 & gt curloc"><img src="content\pic\buttons\sound_off.png"></a></div>'  
 END
END
--- боевой_интерфейс ---------------------------------

