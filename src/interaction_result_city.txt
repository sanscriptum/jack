# interaction_result_city
!handling templates
sp_txt = 1
 
!interaction type - talk
IF $interaction_type = 'standart':
 dynamic $special_part   
END

!interaction type - byer invitation
IF $interaction_type = 'byer_invitation':
 gs '$slave_price'
 npc = 1
 dynamic '$text[1] = $<<$npc_id>>_pref[1]'
 IF npc_progress[$npc_id] = 2: 
  $text[1] = '   [<<$npc_name[$npc_id]>>]
   - I still do not need new slaves. But I said a word to friends, I am sure you will find buyers for such goods without difficulty.'
  else
  IF slave_state = CONST_INT['slave_exist'] and slave['brand'] > 0:
   txt = 1
   $yes = {$interaction_type = 'sell_slave_simple'
   gt 'interaction_result_city'}
   $no = {gt 'city_menu_form'}
   gt 'choice_screen'
  END
 END
END

!interaction type - slave sell simple scheme
IF $interaction_type = 'sell_slave_simple':
 txt_count = 1
 if slave_specialisation[$interesting_specialisation[$npc_id]] => 1 and rating => demand_rating[$npc_id] and slave[$npc_id] = 0: byer_interested = 1 
 n = 13 + 2 * demand_rating[$npc_id]
 demand = rand(0,n)
 if demand > charm and byer_interested = 1: byer_interested += no_charm_check
 IF byer_interested = 1:
  npc_progress[$npc_id] = 2
  $text[1] = $buy_quote[$npc_id]
  sparks += common_price[demand_rating[$npc_id]]
  slave_state = CONST_INT['no_slave']
  master_mood['pos_deal'] = 1
  IF fixed_brand_rise = 0:
  !was brand_rate, which broke leveling the rate of brand completely and did not give leveling
   if brand_reputation <= demand_rating[$npc_id]: brand_rate += demand_rating[$npc_id]
   !if faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: dynamic $rise_faction_fame
   IF faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: 
    no_rise = 0
    if $npc_alignment[$npc_id] = 'taurus' and faction_fame['kamira'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'serpis' and faction_fame['korvus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'kamira' and faction_fame['taurus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'korvus' and faction_fame['serpis'] > 2: no_rise = 1
    if noble_status > 0 and faction_fame[$npc_alignment[$npc_id]] => 4: no_rise = 1
    if no_rise = 0: faction_fame[$npc_alignment[$npc_id]] += 1
   END
   else
   brand_rate += fixed_brand_rise
  END
  else
  if demand > charm: $text[1] = $refuse_charm_quote[$npc_id] 
  if slave_specialisation[$interesting_specialisation[$npc_id]] < 1: $text[1] = $refuse_skill_quote[$npc_id]  
  if demand_rating[$npc_id] > rating: $text[1] = $refuse_level_quote[$npc_id]  
  if slave[$npc_id] = 1: $text[1] = $refuse_again_quote[$npc_id]
  if demand > charm: slave[$npc_id] = 1
 END
END

!interaction type - sell_slave_complex
IF $interaction_type = 'sell_slave_complex':
 txt_count = 1
 gs 'slave_sell_chek'
 IF byer_interested = 1:
  $text[1] = $buy_quote[$npc_id]
  if  noprice = 0: sparks += common_price[demand_rating[$npc_id]]
  slave_state = CONST_INT['no_slave']
  master_mood['pos_deal'] = 3
  !fixed code for kings
  IF demand_rating[$npc_id] = 5:
   brand_rate += 20
   else
   brand_rate += demand_rating[$npc_id]
  END
  
  !faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: dynamic $rise_faction_fame
   IF faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: 
    no_rise = 0
    if $npc_alignment[$npc_id] = 'taurus' and faction_fame['kamira'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'serpis' and faction_fame['korvus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'kamira' and faction_fame['taurus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'korvus' and faction_fame['serpis'] > 2: no_rise = 1
    if noble_status > 0 and faction_fame[$npc_alignment[$npc_id]] => 4: no_rise = 1
    if no_rise = 0: faction_fame[$npc_alignment[$npc_id]] += 1
   END  
  npc_progress[$npc_id] = 2
  active[$quest[5]] = 0
  if noble_status = 0 and demand_rating[$npc_id] = 5: noble_status = 1
  else
  if sl_txt = 103: $text[1] = $refuse_charm_quote[$npc_id]  
  if sl_txt = 102: $text[1] = $refuse_skill_quote[$npc_id]  
  if sl_txt = 101: $text[1] = $refuse_level_quote[$npc_id]  
  if sl_txt = 100: $text[1] = $refuse_special_quote[$npc_id]
 END
END

!interaction type - Bryn teaches the hero with a superb
IF $interaction_type = 'learn_spm':
 $place = 'colloseum'
 $return_lock  = 'next_day'  
 npc = 1
 $npc_id = 'брина_рыжая' 
 txt_count = 2 
 $text[1] = '   [RED HAIR BRYN]
   - C''mon! Stronger!! Faster!!! You fight worse than twelve year old girl, slaver. H-ha, take it! Do you feel it? That''s how it should be. This will feel your enemy! Arise, deadhead! Again. Stronger!! Faster!!!'
 $text[2] = '   Bryn drives you through the training arena all day, and as a result you feel squeezed like a lemon. From strike demonstrations you get bruises all over your body. But she also takes a sparks for that! Well, at least now you know the new powerful technique. Now home. Time to relax...' 
 if $master_spm[1] = '': $master_spm[1] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[2] = '': $master_spm[2] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[3] = '': $master_spm[3] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[4] = '': $master_spm[4] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[5] = '': $master_spm[5] = $spm_to_learn & jump 'spm_loop'  
 :spm_loop
 $spm_learned[$spm_to_learn] = 1
 spm_number += 1
END

!interaction type - sold slave
IF $interaction_type = 'slave_sold':
 personal_relations[$npc_id] += client_statisfaction
 IF client_statisfaction > 0 and npc_bennies[$npc_id] > 0:
  bennies_give = 1 + rating - demand_rating[$npc_id]
  IF bennies_give <=  npc_bennies[$npc_id]:
   brand_rate += bennies_give
   npc_bennies[$npc_id] -= bennies_give
   else
   brand_rate += npc_bennies[$npc_id]
   npc_bennies[$npc_id] = 0
  END
 END
 IF client_statisfaction < 0 and npc_bennies[$npc_id] > 0:
  brand_rate += client_statisfaction
  npc_bennies[$npc_id] += client_statisfaction
  if npc_bennies[$npc_id] < 0: npc_bennies[$npc_id] = 0
 END
 $return_lock = $return_lock_transfer 
 slave_state = CONST_INT['no_slave']
 killvar 'slave'
 killvar '$slave' 
 killvar 'slave_rate'
 $slave_name = '' 
 sparks += sell_price
 master_mood['pos_deal'] = 1
 rr = RAND (0,10)
 no_slave_count[$npc_id] = count_size[$npc_id] + rr
 txt_count = 1
 $place = $place_transfer   
END

!interaction type - demonstration of the apartments for rent
IF $interaction_type = 'rental_demo':
 npc = 1
 $npc_id = 'realtor'
 $return_lock  = 'shop_general'   
 $special_action[3] = {
 $place = 'demo'
 $place_name['demo'] = 'Rooms inspection'
 $place_info['demo'] = '   It is necessary to carefully examine all the rooms, so I do not slipped with some garbage.' 
 $bg['demo'] = $interior_study['<<$demo_var>>']
} 
 $special_action[5] = {npc = 0}
 $special_action[6] = {
npc = 1 
$bg['demo'] = $interior_bed['<<$demo_var>>']
} 
 $special_action[7] = {npc = 0}
 $special_action[8] = {
npc = 1
$bg['demo'] = $interior_bath['<<$demo_var>>']
} 
 $special_action[9] = {npc = 0}
 $special_action[10] = {
npc = 1
$bg['demo'] = $interior_kitchen['<<$demo_var>>']
} 
 $special_action[11] = {npc = 0}
 $special_action[12] = {
npc = 1
$bg['demo'] = $interior_study['<<$demo_var>>']
} 
 $special_action[13] = {
npc = 0 
$bg['demo'] = $interior_bouduar['<<$demo_var>>']
} 
 $special_action[14] = {
$bg['demo'] = $interior_dungeon['<<$demo_var>>']
} 
 $special_action[15] = {
$bg['demo'] = $interior_lab['<<$demo_var>>']
} 
 $special_action[16] = {
npc = 1
$bg['demo'] = $interior_study['<<$demo_var>>']
gt 'choice_screen'
} 
 $no = {
$place = 'realtor_office'
gt 'shop_general'
 }
 txt_count = 16
END

!interaction type - signing rent
IF $interaction_type = 'rental_sex':
 npc = 1
 $npc_id = 'realtor'
 menu_form = 0
 gs 'new_estate'
 $place = 'bedroom'
 $return_lock  = 'menu_form'  
 $special_action[3] = {npc = 0} 
 IF master_excitement > 0:
  txt_count = 6
  $special_image[3] = 'girls\sex\bj_general.gif'
  $special_image[4] = 'girls\sex\bj_general.gif'
  $special_image[5] = 'girls\sex\anal_general.gif'
  $special_image[6] = 'girls\sex\anal_general.gif'
  $play_sound[3] = '<<$bj_sound[2]>>'
  $play_sound[5] = '<<$assfuck_sound[2]>>'  
  ejaculation_intensity = master_excitement
  dynamic $master_ejaculate 
  dynamic $master_energy_drop 
  master_hygiene_rate += 10
  house_mess_rate += 10
  copyarr '$text', '$realtor_bonus_sex'
  else
  dynamic $master_energy_drop 
  txt_count = 4
  cooked_food = 5
  house_mess_rate = 0
  house_mess = 0
  sparks += 5
  copyarr '$text', '$realtor_no_sex'
 END 
END

!interaction type - brothel
IF $interaction_type = 'whore':
 $place = 'brothel'
 $return_lock  = 'shop_general'  
 txt_count = 3
 sparks -= whore_price
 ejaculation_intensity = whore_quality 
 dynamic $master_ejaculate 
 dynamic $master_energy_drop 
 add_master_hygiene = 5 * whore_quality
 master_hygiene_rate += add_master_hygiene
 $picture_type = 'pic_jpg'
 dynamic $special_part  
END

!interaction type - borrow money
IF $interaction_type = 'moneylender':
 $return_lock  = 'shop_general'  
 if nsfw = 1: not_safe = 1
 $place = 'moneylander' 
 $npc_id = 'moneylender'
 npc = 1
 txt_count = 1
 $text[1] = '  [MONEYLENDER]
   - Here''s your sparks, slaver. If you bring back the debt on time, maybe I will increase the amount of your loan. Maybe. But if you can not manage this - I will shorten your life. And this will be for sure. I will waiting for you during <<debt_count>> days. Do not be late.'
 sparks += loan 
END

!interaction type - byu item for current slave
IF $interaction_type = 'buy_item':
 txt_count = 1
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 if nsfw = 0: $special_bg  = "<<$item_demo[$item_id]>>"  
 $text[1] = $item_description[$item_id]
 IF sparks => item_price[$item_id]: 
  $do_buy = {
  sparks -= item_price[$item_id]
  slave_inventory[$item_id] += 1} 
  gt 'item_buy' 
  else
  $text[1] = '   Price is <<item_price[$item_id]>> sparks, slightly more than you can afford at the moment, but this is easily remedied. You just have to sell a couple of slaves...'
 END
END

!interaction type - purchase item to the home
IF $interaction_type = 'buy_item_home':
 txt_count = 1
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 if nsfw = 0: $special_bg  = "<<$item_demo[$item_id]>>"  
 $text[1] = $item_description[$item_id]
 IF sparks => item_price[$item_id]: 
  $do_buy = {
  sparks -= item_price[$item_id]
  inventory[$item_id] += 1} 
  gt 'item_buy' 
  else
  $text[1] = '   Price is <<item_price[$item_id]>> sparks, slightly more than you can afford at the moment, but this is easily remedied. You just have to sell a couple of slaves...'
 END
END

IF $interaction_type = 'buy_item_personal':
 if sound_on = 1: play 'content\snd\kkm.mp3', sound_volume 
 $return_lock  = 'shop_general'  
 sparks -= item_price[$item_id]
 dynamic '<<$item_id>> += multiple' 
 gt 'shop_general'
END

!interaction type - sevice
IF $interaction_type = 'service':
 txt_count = 2
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 $special_bg  = $service_picture   
 IF sparks => item_price[$item_id]: 
  sparks -= service_price
  dynamic $do_service
  else
  txt_count = 1  
  $special_bg  = ''     
  $text[1] = '   The price of the service is <<service_price>> sparks, slightly more than you can afford at the moment, but this is easily remedied. You just have to sell a couple of slaves...'
 END
END

!interaction type - treat a girl at the bar
IF $interaction_type = 'hit_on_patron':
 $place = 'tabern'
 IF already_done_today[interaction_city] <= 0:
  a = RAND (1,2)
  if a = 1: $npc_id = 'брина_рыжая' 
  if a = 2: $npc_id = 'poison' 
  npc = 1
  dynamic $npc_flirt[$npc_id]
  npc_reveal[$npc_id] = 1
  sparks -= 2
  else
  txt_count = 1
  $text[1] = '   You are viewing the pub in search of other bored and lonely women, but no suitable candidate for courtship is visible. Probably should try your luck tomorrow.'
 END  
END

!completion tags
already_done_today[interaction_city] += 1 
already_done_ever[interaction_city] += 1 

txt = 1
GT 'interaction_screen_city'
--- interaction_result_city ---------------------------------

